pipeline {
  agent {
    kubernetes {
      yaml '''
apiVersion: "v1"
kind: "Pod"
metadata:
  labels:
    jenkins: "agent"
    job: "plugin-site"
spec:
  tolerations:
    - key: "os"
      operator: "Equal"
      value: "linux"
      effect: "NoSchedule"
    - key: "jenkins"
      operator: "Equal"
      value: "infra.ci.jenkins.io"
      effect: "NoSchedule"
    - key: "kubernetes.azure.com/scalesetpriority"
      operator: "Equal"
      value: "spot"
      effect: "NoSchedule"
  affinity:
    nodeAffinity:
      requiredDuringSchedulingIgnoredDuringExecution:
        nodeSelectorTerms:
          - matchExpressions:
              - key: kubernetes.io/os
                operator: In
                values:
                  - linux
  restartPolicy: "Never"
  automountServiceAccountToken: false
  containers:
    - name: "jnlp"
      image: "jenkinsciinfra/builder:3.0.19@sha256:e813bb241eaea74fa59a1c18596c626a8a5d6ff09b49efd8359f7a0340221845"
      resources:
        limits: {}
        requests:
          memory: "4Gi"
          cpu: "2"
      '''
    }
  }

  environment {
    TZ = "UTC"
    GET_CONTENT = "true"
    NODE_ENV = "production"
    // TODO: Remove this "custom PATH" once https://github.com/jenkins-infra/docker-builder/blob/e5749f5cf0392549a89ba3a5b41a41fe55ec48dd/Dockerfile#L46 is updated to persist it
    PATH = "/home/jenkins/.local/bin:${env.PATH}"
  }

  triggers {
    cron("${env.BRANCH_NAME == 'master' ? 'H H/3 * * *' : ''}")
  }

  options {
    timeout(time: 60, unit: 'MINUTES')
    ansiColor('xterm')
    disableConcurrentBuilds(abortPrevious: true)
    buildDiscarder logRotator(artifactDaysToKeepStr: '', artifactNumToKeepStr: '', daysToKeepStr: '5', numToKeepStr: '5')
    retry(5)
  }

  stages {
    stage('Install Dependencies') {
      steps {
        sh 'asdf install'
        sh 'NODE_ENV=development yarn install'
      }
    }

    stage('Build PR') {
      when {
        not {
          branch "master"
        }
      }
      environment {
        DISABLE_SEARCH_ENGINE = "true"
        GATSBY_ALGOLIA_APP_ID = credentials('algolia-plugins-app-id')
        GATSBY_ALGOLIA_SEARCH_KEY = credentials('algolia-plugins-search-key')
        NETLIFY = "true"
      }
      steps {
        sh 'yarn build'
      }
    }

    stage('Build Production') {
      when {
        branch "master"
      }
      environment {
        GATSBY_MATOMO_SITE_ID = "1"
        GATSBY_MATOMO_SITE_URL = "https://jenkins-matomo.do.g4v.dev"
        GATSBY_ALGOLIA_APP_ID = credentials('algolia-plugins-app-id')
        GATSBY_ALGOLIA_SEARCH_KEY = credentials('algolia-plugins-search-key')
        GATSBY_ALGOLIA_WRITE_KEY = credentials('algolia-plugins-write-key')
      }
      steps {
        sh 'yarn build'
      }
    }

    stage('Check build') {
      when { expression { return !fileExists('./plugins/plugin-site/public/index.html') } }
      steps {
        error("Something went wrong, index.html was not generated")
      }
    }

    stage('Lint and Test') {
      environment {
        NODE_ENV = "development"
        NODE_OPTIONS="--experimental-vm-modules"
      }
      steps {
        sh '''
          yarn lint
          yarn test
        '''
      }
    }

    stage('Deploy to azure') {
      when {
        branch "master"
      }
      environment {
        PLUGINSITE_STORAGEACCOUNTKEY = credentials('PLUGINSITE_STORAGEACCOUNTKEY')
      }
      steps {
        sh('''
            blobxfer upload \
          --local-path ./plugins/plugin-site/public \
          --storage-account-key $PLUGINSITE_STORAGEACCOUNTKEY \
          --storage-account prodpluginsite \
          --remote-path pluginsite \
          --recursive \
          --mode file \
          --skip-on-md5-match \
          --file-md5 \
          --connect-timeout 30 \
          --delete
        ''')
      }
    }

    stage('Deploy to preview site') {
      when {
        changeRequest target: 'master'
      }
      environment {
        NETLIFY_AUTH_TOKEN = credentials('netlify-auth-token')
      }
      post {
        success {
          recordDeployment('jenkins-infra', 'plugin-site', pullRequest.head, 'success', "https://deploy-preview-${CHANGE_ID}--jenkins-plugin-site-pr.netlify.app")
        }
        failure {
          recordDeployment('jenkins-infra', 'plugin-site', pullRequest.head, 'failure', "https://deploy-preview-${CHANGE_ID}--jenkins-plugin-site-pr.netlify.app")
        }
      }
      steps {
        sh('/usr/local/bin/netlify-deploy --siteName "jenkins-plugin-site-pr" --title "Preview deploy for ${CHANGE_ID}" --alias "deploy-preview-${CHANGE_ID}" -d ./plugins/plugin-site/public')
      }
    }
  }
}

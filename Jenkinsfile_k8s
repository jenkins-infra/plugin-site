def recordDeployment(owner, repo, status, environmentURL, environment = "preview", description = "Deploy to preview environment") {
  withCredentials([usernamePassword(credentialsId: 'github-app-infra', usernameVariable: 'GITHUB_APP', passwordVariable: 'GH_TOKEN')]) {
    def json = writeJSON(returnText: true, json: [
        "ref": env.GIT_COMMIT,
        "environment": environment,
        "description": description,
        "required_contexts":[],
        "auto_merge":false,
        "transient_environment":false,
    ])
    def id = readJSON(text: sh(script: "gh api repos/${owner}/${repo}/deployments  -X POST --input - << EOF\n${json}\nEOF", returnStdout: true).trim()).id
    if (id == ''){
      error('Unable to create deployment')
    }
    json = writeJSON(returnText: true, json: [
        "state": status,
        "environment": environment,
        "description": description,
        "log_url": "${BUILD_URL}/console",
        "environment_url": environmentURL,
    ])
    sh("gh api repos/${owner}/${repo}/deployments/${id}/statuses  -X POST --input - << EOF\n${json}\nEOF")
  }
}

pipeline {
  agent {
    kubernetes {
      inheritFrom 'jnlp-linux'
      yaml '''
apiVersion: "v1"
kind: "Pod"
metadata:
  labels:
    jenkins: "agent"
    job: "plugin-site"
spec:
  tolerations:
    - key: "os"
      operator: "Equal"
      value: "linux"
      effect: "NoSchedule"
  affinity:
    nodeAffinity:
      requiredDuringSchedulingIgnoredDuringExecution:
        nodeSelectorTerms:
          - matchExpressions:
              - key: kubernetes.io/os
                operator: In
                values:
                  - linux
  restartPolicy: "Never"
  containers:
    - name: "node"
      image: "node:14.17"
      securityContext:
        privileged: false
      tty: true
      command:
      - cat
    - name: "blobxfer"
      image: "mcr.microsoft.com/blobxfer:1.9.1"
      command:
      - cat
      securityContext:
        privileged: false
      tty: true
    - name: "builder"
      image: "jenkinsciinfra/builder:1.2.0"
      command:
      - cat
      securityContext:
        privileged: false
      tty: true
      '''
    }
  }

  environment {
    TZ = "UTC"
    GET_CONTENT = "true"
    NODE_ENV = "production"
    HOME = "/tmp"
  }

  triggers {
    cron("${env.BRANCH_NAME == 'master' ? 'H H/3 * * *' : ''}")
  }

  options {
    timeout(time: 60, unit: 'MINUTES')
    ansiColor('xterm')
    disableConcurrentBuilds()
    buildDiscarder logRotator(artifactDaysToKeepStr: '', artifactNumToKeepStr: '', daysToKeepStr: '5', numToKeepStr: '5')
  }

  stages {
    stage('NPM Install') {
      steps {
        container('node') {
          sh 'NODE_ENV=development yarn install'
        }
      }
    }

    stage('Build PR') {
      when {
        not {
          branch "master"
        }
      }
      environment {
        DISABLE_SEARCH_ENGINE = "true"
        NETLIFY = "true"
      }
      steps {
        container('node') {
          sh 'yarn build'
        }
      }
    }

    stage('Build Production') {
      when {
        branch "master"
      }
      environment {
        GATSBY_MATOMO_SITE_ID = "1"
        GATSBY_MATOMO_SITE_URL = "https://jenkins-matomo.do.g4v.dev"
        GATSBY_SENTRY_DSN = "https://495b0bd32a5e4a2287c3fe4b061ee24f@sentry.io/1882460"
        GATSBY_ALGOLIA_APP_ID = credentials('algolia-plugins-app-id')
        GATSBY_ALGOLIA_SEARCH_KEY = credentials('algolia-plugins-search-key')
        GATSBY_ALGOLIA_WRITE_KEY = credentials('algolia-plugins-write-key')
      }
      steps {
        container('node') {
          sh 'yarn build'
        }
      }
    }

    stage('Check build') {
      when { expression { return !fileExists('./plugins/plugin-site/public/index.html') } }
      steps {
        error("Something went wrong, index.html was not generated")
      }
    }

    stage('Lint and Test') {
      environment {
        NODE_ENV = "development"
      }
      steps {
        container('node') {
          sh 'yarn lint'
          sh 'yarn test'
        }
      }
    }

    stage('Deploy to azure') {
      when {
        branch "master"
      }
      environment {
        PLUGINSITE_STORAGEACCOUNTKEY = credentials('PLUGINSITE_STORAGEACCOUNTKEY')
      }
      steps {
        container('blobxfer') {
          sh('''
              blobxfer upload \
            --local-path ./plugins/plugin-site/public \
            --storage-account-key $PLUGINSITE_STORAGEACCOUNTKEY \
            --storage-account prodpluginsite \
            --remote-path pluginsite \
            --recursive \
            --mode file \
            --skip-on-md5-match \
            --file-md5 \
            --connect-timeout 30 \
            --delete
          ''')
        }
      }
    }

    stage('Deploy to preview site') {
      when {
        changeRequest target: 'master'
      }
      environment {
        NETLIFY_AUTH_TOKEN = credentials('netlify-auth-token')
      }
      post {
        success {
          container('builder') {
            recordDeployment('jenkins-infra', 'plugin-site', 'success', "https://deploy-preview-${CHANGE_ID}--jenkins-plugin-site-pr.netlify.app")
          }
        }
        failure {
          container('builder') {
            recordDeployment('jenkins-infra', 'plugin-site', 'failure', "https://deploy-preview-${CHANGE_ID}--jenkins-plugin-site-pr.netlify.app")
          }
        }
      }
      steps {
        container('node') {
          script {
            def netlifySite = readJSON(text: sh(script: 'yarn exec netlify sites:list --json', returnStdout: true).trim()).find { it.name == 'jenkins-plugin-site-pr' }
            env.NETLIFY_SITE_ID = netlifySite['site_id']
            env.NETLIFY_DOMAIN = netlifySite['default_domain']

            sh('yarn exec netlify deploy --message "Preview deploy for ${CHANGE_ID}" --alias "deploy-preview-${CHANGE_ID}" -d ./plugins/plugin-site/public')
          }
        }
      }
    }
  }
}
